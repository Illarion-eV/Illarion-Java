/*
 * This file is part of the Illarion project.
 *
 * Copyright Â© 2015 - Illarion e.V.
 *
 * Illarion is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Illarion is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */
import de.undercouch.gradle.tasks.download.Download

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply from: '../versions.gradle'

archivesBaseName = 'illarion_common'

dependencies {
    api group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4jVersion
    api group: 'org.slf4j', name: 'log4j-over-slf4j', version: project.ext.slf4jVersion
    api group: 'org.slf4j', name: 'jcl-over-slf4j', version: project.ext.slf4jVersion
    implementation group: 'org.ogce', name: 'xpp3', version: '1.1.6'
    implementation group: 'org.bushe', name: 'eventbus', version: '1.4'
    implementation group: 'biz.futureware.mantis', name: 'mantis-axis-soap-client', version: '1.2.19'
    api 'com.google.code.findbugs:jsr305:3.0.2'
}

processResources.dependsOn("downloadSkills")

task downloadSkills(type: Download) {
    onlyIf { !gradle.startParameter.isOffline() }
    if (rootProject.hasProperty('releaseBuild')) {
        src 'https://illarion.org/data/xml_skills.php?server=1'
    } else {
        src 'https://illarion.org/data/xml_skills.php'
    }
    dest project.projectDir.path + '/src/main/resources/skills.xml'
    onlyIfNewer true
}

jar {
    dependsOn { gitVersion }
    manifest {
        attributes(
                'Permissions': 'all-permissions',
                'Codebase': 'illarion.org',
                'Trusted-Library': true,
                'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Implementation-Title': 'Illarion Common Library',
                'Implementation-Vendor': 'Illarion e.V.',
                'Implementation-Version': project.ext.fullVersion
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url project.ext.mavenRepo
        }
    }
}
