/*
 * This file is part of the Illarion project.
 *
 * Copyright Â© 2015 - Illarion e.V.
 *
 * Illarion is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Illarion is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */

apply plugin: 'idea'

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

idea {
    project {
        languageLevel = '1.8'
    }
}

allprojects {
    if (project.hasProperty('targetRepo')) {
        ext.mavenRepo = "file://$targetRepo"
    } else {
        ext.mavenRepo = "file://${project.buildDir}/repo"
    }

    task gitVersion { task ->
        def gitVersion = 'unknown'
        def branch = 'develop'
        def File rootDir = project.rootDir
        for (def gitCmd in ['git', 'git.cmd']) {
            try {
                def procBranch = "$gitCmd rev-parse --abbrev-ref HEAD".execute(null as List, rootDir)
                procBranch.waitFor()
                branch = procBranch.in.text.trim()
                procBranch.destroy()

                def procVersion = "$gitCmd describe --tags HEAD".execute(null as List, rootDir)
                procVersion.waitFor()
                gitVersion = procVersion.in.text.trim()
                procVersion.destroy()

                break
            } catch (e) {
                logger.error('Failed to get version from Git', e)
            }
        }

        def String mainVersion
        def indexOfSeparator = gitVersion.indexOf('-')
        if (indexOfSeparator == -1) {
            mainVersion = gitVersion
        } else {
            mainVersion = gitVersion.substring(0, indexOfSeparator)
        }

        if (branch == 'master') {
            if (indexOfSeparator > -1) {
                throw new RuntimeException("Can't build snapshot in master branch.")
            }
        } else {
            mainVersion += '-SNAPSHOT'
        }

        task.project.version = mainVersion
        task.project.ext.fullVersion = gitVersion
    }

    configurations.all {
        resolutionStrategy {
            cacheDynamicVersionsFor 10 * 60, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }

    repositories {
        mavenCentral()
        maven {
            url 'http://illarion.org/media/java/maven'
        }
        maven {
            url 'http://oss.sonatype.org/content/repositories/releases'
        }
    }
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://maven.ej-technologies.com/repository'
        }
    }
    dependencies {
        classpath group: 'net.sf.proguard', name: 'proguard-gradle', version: '5.1'
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '2.0.0'
        classpath group: 'com.install4j', name: 'gradle-plugin', version: '6.0.4'
    }
}

subprojects {
    group = 'org.illarion'

    plugins.withType(JavaPlugin) {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }

        test {
            useTestNG()
            options {
                listeners.clear()
                listeners << 'org.testng.reporters.XMLReporter'
            }
        }

        configurations {
            provided
        }
        sourceSets {
            main.compileClasspath += configurations.provided
            test.compileClasspath += configurations.provided
            test.runtimeClasspath += configurations.provided
        }
        dependencies {
            compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.3'
        }
    }

    apply plugin: 'idea'
    apply plugin: 'findbugs'
    apply plugin: 'pmd'
    findbugs {
        ignoreFailures = true
        effort = "max"
        reportLevel = "high"
    }
    pmd {
        ignoreFailures = true
        targetJdk = '1.7'
        ruleSetFiles = files(new File(rootDir, 'pmd.xml'))
        ruleSets = []
    }
    idea {
        module {
            if (scopes.PROVIDED != null) {
                scopes.PROVIDED.plus += configurations.provided
            }
        }
    }
}