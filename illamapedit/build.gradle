/*
 * This file is part of the Illarion Client.
 *
 * Copyright Â© 2013 - Illarion e.V.
 *
 * The Illarion Client is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * The Illarion Client is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with the Illarion Client.  If not, see <http://www.gnu.org/licenses/>.
 */
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'

apply from: '../versions.gradle'

archivesBaseName = 'illarion_mapedit'
mainClassName = 'illarion.mapedit.MapEditor'

dependencies {
    compile project(':common')
    compile project(':engine')
    compile group: 'org.javolution', name: 'javolution-core-java', version: '6.0.0'
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '2.0.3'
    compile group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4jVersion
    compile group: 'org.slf4j', name: 'jul-to-slf4j', version: project.ext.slf4jVersion
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: project.ext.slf4jVersion
    compile group: 'org.slf4j', name: 'log4j-over-slf4j', version: project.ext.slf4jVersion
    compile group: 'org.codehaus.janino', name: 'janino', version: '2.6.1'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.13'
    compile group: 'net.sf.trove4j', name: 'trove4j', version: '3.0.3'
    compile group: 'org.bushe', name: 'eventbus', version: '1.4'
    compile group: 'com.github.insubstantial', name: 'substance', version: '7.2.1'
    compile group: 'com.github.insubstantial', name: 'substance-flamingo', version: '7.2.1'
    compile group: 'org.swinglabs', name: 'swingx', version: '1.6.1'
    runtime group: 'org.illarion', name: 'rsc_items', version: project.ext.illarionResourcesVersion
    runtime group: 'org.illarion', name: 'rsc_music', version: project.ext.illarionResourcesVersion
    runtime group: 'org.illarion', name: 'rsc_tables', version: project.ext.illarionResourcesVersion
    runtime group: 'org.illarion', name: 'rsc_tiles', version: project.ext.illarionResourcesVersion
    testCompile group: 'org.testng', name: 'testng', version: '6.8.7'
    testCompile group: 'org.powermock', name: 'powermock-module-testng', version: '1.5.4'
    testCompile group: 'org.powermock', name: 'powermock-api-easymock', version: '1.5.4'
    testCompile group: 'org.easymock', name: 'easymock', version: '3.2'
}

jar {
    dependsOn { gitVersion }
    manifest {
        attributes        \
               'Main-Class': 'illarion.mapedit.MapEditor',      \
               'Permissions': 'all-permissions',       \
               'Codebase': 'illarion.org',       \
               'Application-Name': 'Illarion Mapeditor',       \
               'Application-Library-Allowable-Codebase': 'illarion.org',       \
               'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),       \
               'Built-By': System.getProperty('user.name'),        \
               'Built-JDK': System.getProperty('java.version'),        \
               'Implementation-Title': 'Illarion Mapeditor',        \
               'Implementation-Vendor': 'Illarion e.V.',       \
               'Implementation-Version': project.ext.fullVersion
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url project.ext.mavenRepo
        }
    }
}