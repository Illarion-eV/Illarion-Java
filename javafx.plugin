/*
 * Bootstrap script for the Gradle JavaFX Plugin.
 * (based on http://plugins.jasoft.fi/vaadin.plugin)
 *
 * The script will add the latest version of the plugin to the build script
 * dependencies and apply the plugin to the project. If you do not want
 * this behavior you can copy and paste the below configuration into your
 * own build script and define your own repository and version for the plugin.
 */

buildscript {
    repositories {
        mavenLocal()
        maven {
            name = 'BinTray'
            url = 'http://dl.bintray.com/content/shemnon/javafx-gradle/'
        }
        maven {
            name = 'CloudBees Snapshot'
            url = 'http://repository-javafx-gradle-plugin.forge.cloudbees.com/snapshot'
        }
        ivy {
            url = 'http://repository-javafx-gradle-plugin.forge.cloudbees.com/snapshot'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'org.bitbucket.shemnon.javafxplugin:gradle-javafx-plugin:0.4.0'
        classpath project.files("${System.properties['java.home']}/../lib/ant-javafx.jar")
        switch (JavaVersion.current()) {
            case JavaVersion.VERSION_1_8:
                classpath project.files("${System.properties['java.home']}/lib/ext/jfxrt.jar")
                break
            case JavaVersion.VERSION_1_7:
            case JavaVersion.VERSION_1_6:
                classpath project.files("${System.properties['java.home']}/lib/jfxrt.jar")
                break
            default:
                 throw new GradleException("Java version too old.");
        }
    }
}

if (!project.plugins.findPlugin(org.bitbucket.shemnon.javafxplugin.JavaFXPlugin)) {
    project.apply(plugin: org.bitbucket.shemnon.javafxplugin.JavaFXPlugin)
}
