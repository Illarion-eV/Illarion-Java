/*
 * This file is part of the Illarion project.
 *
 * Copyright Â© 2015 - Illarion e.V.
 *
 * Illarion is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Illarion is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 */
import proguard.gradle.ProGuardTask

apply plugin: 'java'
apply plugin: 'application'
apply from: '../versions.gradle'

archivesBaseName = 'illarion_compiler'
mainClassName = 'illarion.compile.Compiler'

dependencies {
    compile project(':common')
    compile project(':easynpc')
    compile project(':easyquest')
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.slf4j', name: 'slf4j-api', version: project.ext.slf4jVersion
    compile group: 'org.slf4j', name: 'jcl-over-slf4j', version: project.ext.slf4jVersion
    compile group: 'ch.qos.logback', name: 'logback-classic', version: project.ext.logbackVersion
}

jar {
    dependsOn { gitVersion }
    manifest {
        attributes       \
               'Main-Class': 'illarion.compile.Compiler',       \
              'Permissions': 'all-permissions',      \
              'Codebase': 'illarion.org',      \
              'Application-Name': 'Illarion Compiler',      \
              'Application-Library-Allowable-Codebase': 'illarion.org',      \
              'Built-Date': new Date().format('yyyy-MM-dd HH:mm:ssZ'),      \
              'Built-By': System.getProperty('user.name'),       \
              'Built-JDK': System.getProperty('java.version'),       \
              'Implementation-Title': 'Illarion Compiler',       \
              'Implementation-Vendor': 'Illarion e.V.',      \
              'Implementation-Version': project.ext.fullVersion
    }
}

task proguard(type: ProGuardTask, dependsOn: 'jar') {
    final def mainFile = new File(project.buildDir, "libs/${project.archivesBaseName}-${project.version}.jar")
            .absolutePath
    injars(mainFile)
    configurations.runtime.each { File file ->
        if (file.absolutePath.startsWith(System.getProperty('java.home'))) {
            libraryjars file.absolutePath
        } else {
            def filter = "!META-INF/*,!META-INF/plexus/*,!META-INF/sisu/*,!META-INF/maven/**,!about.html,!LICENSE," +
                    "!OSGI-OPT/**,!**/packageinfo,!**.java,!xmlns/**"
            if (file.absolutePath.contains("xpp3") || file.absolutePath.contains("axis-jaxrpc")) {
                filter += ",!javax/xml/namespace/QName.class"
            }
            if (file.absolutePath.contains("illarion")) {
                filter += ",!*.png,!logback.xml"
                filter += ",!Books.dat"
                filter += ",!Chars.dat"
                filter += ",!Cloth.dat"
                filter += ",!Effects.dat"
                filter += ",!Gui.dat"
                filter += ",!Overlays.dat"
                filter += ",!Runes.dat"
                filter += ",!Sounds.dat"
                filter += ",!Tiles.dat"
            }
            if (file.absolutePath.contains("javolution-core-java")) {
                filter += ",!org/osgi/**"
            }
            injars([filter: filter], file.absolutePath)
        }
    }

    libraryjars files("${System.getProperty('java.home')}/lib/jce.jar")
    libraryjars files("${System.getProperty('java.home')}/lib/rt.jar")

    outjars new File(project.buildDir, "compiler.jar").absolutePath

    /* Forcing all class files to contain the same codebase level. */
    target "1.7"

    /* Optimization settings */
    dontoptimize
    optimizationpasses 1
    allowaccessmodification
    mergeinterfacesaggressively

    /* Obfuscation settings */
    dontobfuscate

    renamesourcefileattribute "SourceFile"
    keepattributes "SourceFile,LineNumberTable"
    keepattributes "*Annotation*"

    keepclasseswithmembers "public class illarion.compile.Compiler {" +
            "public static void main(java.lang.String[]);" +
            "}"

    keepclasseswithmembernames "class * {" +
            "native <methods>;" +
            "}"

    keepclassmembers "class * {" +
            "@illarion.common.util.CalledByReflection *;" +
            "}"

    keepclassmembers "class * extends java.lang.Enum {" +
            "public static **[] values();" +
            "public static ** valueOf(java.lang.String);" +
            "}"

    keepclassmembers "class * implements java.io.Serializable {" +
            "static final long serialVersionUID;" +
            "static final java.io.ObjectStreamField[] serialPersistentFields;" +
            "private void writeObject(java.io.ObjectOutputStream);" +
            "private void readObject(java.io.ObjectInputStream);" +
            "java.lang.Object writeReplace();" +
            "java.lang.Object readResolve();" +
            "}"

    keep "class ch.qos.logback.** { *; }"
    keep "class illarion.compile.impl.EasyQuestCompile"
    keep "class illarion.compile.impl.EasyNpcCompile"
    keep "class org.xmlpull.mxp1.MXParserFactory"

    dontwarn "com.sun.**"
    dontwarn "javax.servlet.**"
    dontwarn "javax.mail.**"
    dontwarn "javax.jms.**"
    dontwarn "javax.xml.**"
    dontwarn "org.pushingpixels.**"
    dontwarn "org.apache.commons.**"
    dontwarn "org.apache.axis.**"
    dontwarn "org.mantisbt.**"
    dontwarn "org.osgi.service.**"
    dontnote "javax.xml.**"
    dontwarn "ch.qos.logback.classic.**"
    dontwarn "org.codehaus.janino.**"
    dontwarn "org.apache.log4j.**"
}


if (project.hasProperty('compilerDir')) {
    task publishBundle(type: Copy, dependsOn: 'proguard') {
        from new File(project.buildDir, "compiler.jar").absolutePath
        into file(project.compilerDir)
    }
    task publish(dependsOn: "publishBundle") {}
}

build.dependsOn("proguard")